/**
 * Standard plugins for the Gradle builder
 */
plugins {
    id 'java'
}

/**
 * Project group and version
 */
group = 'io.xlorey.plugintemplate'
version = '1.0.0'

/**
 * Repositories used
 */
repositories {
    mavenCentral()
}

/**
 * Collector Variables
 */
ext {
    buildPath = System.getenv('BUILD_DIR') ?: 'build';
    fileName = "${rootProject.name}-${version}.jar"
}

/**
 * Plugin Dependencies
 */
dependencies {
    /**
     * Flux Loader - connects only in compileOnly format
     */
    compileOnly files("libs/FluxLoader-0.10.0.jar")

    /**
     * Project Zomboid library
     */
    compileOnly files("libs/zombie.jar")
}

/**
 * Updating project metadata
 */
processResources {
    inputs.property "version", project.version

    filesMatching("metadata.json") {
        expand "version": project.version
    }
}

/**
 * Build the project in a jar archive. The source file will be created in the 'build' folder
 */
jar {
    archiveFileName = project.ext.fileName;
    destinationDirectory = file(project.ext.buildPath)
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    from ('.') {
        include('LICENSE')
    }
}